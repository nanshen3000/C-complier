S -> program $

program ->  var_declarations statements
program -> statements

var_declarations -> var_declarations var_declaration 

var_declarations -> var_declaration 

var_declaration  ->  INT declaration_list SEMI

declaration_list ->  declaration_list COMMA declaration 
   declaration_list -> declaration

declaration -> ID ASSIGN INT_NUM 
   declaration -> ID LSQUARE INT_NUM RSQUARE 
   declaration -> ID

code_block -> statement 
   code_block -> LBRACE statements RBRACE

statements ->  statements statement 
   statements -> statement

statement -> assign_statement SEMI 
   statement -> control_statement 
   statement -> read_write_statement SEMI 
   statement -> SEMI

control_statement -> if_statement 
   control_statement -> while_statement 
   control_statement -> do_while_statement  SEMI 
   control_statement -> return_statement SEMI

read_write_statement  ->  read_statement 
    read_write_statement  ->  write_statement

assign_statement  ->  ID LSQUARE exp RSQUARE ASSIGN exp 
    assign_statement  -> ID ASSIGN exp

if_statement  -> if_stmt 
    if_statement  -> if_stmt ELSE code_block

if_stmt  ->  IF LPAR exp RPAR code_block

while_statement  ->  WHILE LPAR exp RPAR code_block

do_while_statement  ->  DO code_block WHILE LPAR exp RPAR

return_statement  ->  RETURN

read_statement ->  READ LPAR ID RPAR

write_statement  ->  WRITE LPAR exp RPAR

exp ->  LPAR exp RPAR 
exp ->  INT_NUM 
exp ->  ID 
exp ->  ID LSQUARE exp RSQUARE 

exp ->  NOT_OP exp 
exp ->  MINUS exp

exp ->  exp MUL_OP exp 
exp ->  exp DIV_OP exp 

exp ->  exp PLUS exp 
exp ->  exp MINUS exp 

exp -> exp SHL_OP exp 
exp ->  exp SHR_OP exp 

exp -> exp LT exp 
exp -> exp GT exp 
exp ->  exp LTEQ exp 
exp ->  exp GTEQ exp 

exp -> exp EQ exp 
exp ->  exp NOTEQ exp 

exp ->  exp AND_OP exp 

exp ->  exp OR_OP exp 

exp ->  exp ANDAND exp 

exp ->  exp OROR exp 
